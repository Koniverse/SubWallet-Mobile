// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "35.0.0"
        minSdkVersion = 24
        compileSdkVersion = 35
        targetSdkVersion = 35
        androidXBrowser = "1.4.0"
        kotlinVersion = "1.9.0"
        ndkVersion = "23.1.7779620"
    }
    subprojects { subproject ->
        afterEvaluate{
            if((subproject.plugins.hasPlugin('android') || subproject.plugins.hasPlugin('android-library'))) {
                android {
                    compileSdkVersion rootProject.ext.compileSdkVersion
                    buildToolsVersion rootProject.ext.buildToolsVersion
                }
            }
        }
    }
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.1.1")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.0")
        classpath('org.mozilla.rust-android-gradle:plugin:0.9.3')
    }
}

// Force all modules to use the same Kotlin version
allprojects {
    configurations.all {
        resolutionStrategy {
            force "org.jetbrains.kotlin:kotlin-stdlib:1.9.0"
            force "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.9.0"
            force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.0"
            force "org.jetbrains.kotlin:kotlin-reflect:1.9.0"
            // Force Okio compatible with Kotlin 1.9
            force "com.squareup.okio:okio:3.6.0"

            // Force OkHttp 4.x (last before Kotlin 2.2 bump)
            force "com.squareup.okhttp3:okhttp:4.12.0"
            force "com.squareup.okhttp3:okhttp-urlconnection:4.12.0"
        }
    }
}

apply plugin: "com.facebook.react.rootproject"
